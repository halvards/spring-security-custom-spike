buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE'
    }
}

apply plugin: 'application'                  // http://www.gradle.org/docs/current/userguide/application_plugin.html
apply plugin: 'java'                         // http://www.gradle.org/docs/current/userguide/java_plugin.html
apply plugin: 'spring-boot'                  // http://docs.spring.io/spring-boot/docs/current/reference/html/build-tool-plugins-gradle-plugin.html

def javaVersion = '1.8'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

mainClassName = 'com.skogsrud.halvard.spring.security.custom.spike.Application'

def buildLabel = "${System.env.USER}.${new Date().format("yyyyMMddHHmmss")}"
def travisBuildLabel = System.env.TRAVIS_BUILD_NUMBER
if (travisBuildLabel) {
    buildLabel = travisBuildLabel
}

group = 'com.skogsrud.halvard'
version = buildLabel

configurations {
    all*.exclude group: 'commons-logging'              // Never include commons-logging (use SLF4J instead)
    all*.exclude group: 'log4j'                        // Never include log4j (use SLF4J instead)
    all*.exclude module: 'spring-boot-starter-tomcat'  // Never include Tomcat (use Jetty instead)
    all*.exclude module: 'jetty-jsp'                   // No need for JSP support
    all*.exclude module: 'slf4j-simple'                // Use logback
}

repositories {
    mavenCentral()
}

def hamcrestVersion = '1.3'
def jettyVersion = '9.3.6.v20151106'
def jmustacheVersion = '1.11'
def junitVersion = '4.12'
def mockwebserverVersion = '2.5.0'
def okhttpVersion = '2.6.0'
def springBootVersion = '1.3.0.RELEASE'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}",
            "org.eclipse.jetty:jetty-webapp:${jettyVersion}",
            "com.samskivert:jmustache:${jmustacheVersion}",
            "org.springframework.boot:spring-boot-starter-security:${springBootVersion}",
            "com.squareup.okhttp:okhttp:${okhttpVersion}"

    testCompile "junit:junit:${junitVersion}",
            "org.hamcrest:hamcrest-library:${hamcrestVersion}",
            "org.springframework.boot:spring-boot-starter-test:${springBootVersion}",
            "com.squareup.okhttp:mockwebserver:${mockwebserverVersion}"
}

task stage(dependsOn: ['clean', 'assemble'],
        description: 'Heroku uses this task to build the app.',
        group: 'Build')

task wrapper(type: Wrapper,
        description: 'Create Gradle wrapper scripts.',
        group: 'Build Setup') {
    gradleVersion = '2.9'
}
